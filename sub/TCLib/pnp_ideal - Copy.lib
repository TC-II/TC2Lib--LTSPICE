* Component Created by Javier Petrucci and Agustin Gullino
* jpetrucc@itba.edu.ar / agullino@itba.edu.ar
*
* This is the PNP BJT
*
*         C
*     |  /
*     | /
* B---| _
*     ||\
*     |  \
*          E
*
*
*  /\Ic
*  |
*  |
*  |        /--------------------
*  |       /
*  |      /----------------------
*  |     /
*  |    /------------------------
*  |   /
*  |  /--------------------------
*  | /
*  |/__________________________________> Veb
*
*

.SUBCKT PNP_nice C B E



**Voltage sources used to measure current trough terminals
V_Ib Vbint B 0
V_Ic Vcint C 0
V_Ie E Veint 0

**Base emiter and base colector voltage drop
**Emiter base diode
D5 Veint Vbint Dideal_Veb
**Colector base diode
D6 Vcint Vbint Dideal_Vcb


**Resistor value according to hfe for forward and inverse current
**Ratio between direct and inverse modes is harcoded here :)
Rf1 Vcint Vcr R = limit(1u,100Meg, abs(   (     ( V(Vcint,Vcr)*V(Vcint,Vcr)+1m ) / V(Vcint,Vcr)        ) /  ( I(V_Ib)*hfe   ) ) )
Rr1 Veint Ver R = limit(1u,100Meg, abs(   (     ( V(Veint,Ver)*V(Veint,Ver)+1m ) / V(Veint,Ver)        )  / ( I(V_Ib)*hfe/20) ) )

*Rf1 Vcint Vcr R = limit(1u,100Meg, abs(   (     									V(Vcint,Vcr)        ) /  ( I(V_Ib)*hfe   ) ) )
*Rr1 Veint Ver R = limit(1u,100Meg, abs(   (     									V(Veint,Ver)        )  / ( I(V_Ib)*hfe/20) ) )


**Saturation voltage
D2 Vx Ver Dideal_Vec
D7 Vx Vcr Dideal_Vec

**Mode selection, forward/inverse
D1 Veint Vx Dideal
D8 Vcint Vx Dideal

**Models for the diodes
.model Dideal D(Ron=10u Vfwd=10m)
.model Dideal_Veb D(Ron=10m Vfwd={Veb})
.model Dideal_Vec D(Ron=10u Vfwd={Vec_sat})
.model Dideal_Vcb D(Ron=10u Vfwd={Veb*0.98})



.end PNP_nice
*